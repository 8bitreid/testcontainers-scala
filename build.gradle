buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.12.0'
    }
}

plugins {
    id 'nebula.provided-base' version '3.0.3'
    id 'net.researchgate.release' version '2.4.0'
}

group 'com.dimafeng'

apply plugin: 'scala'
apply plugin: 'nebula.provided-base'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'maven'
apply plugin: 'signing'


sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile(
            "org.scala-lang:scala-library:$scalaVersion",
            "org.testcontainers:testcontainers:$testcontainersVersion"

    )
    provided("org.seleniumhq.selenium:selenium-java:$seleniumVersion",
            "org.testcontainers:selenium:$testcontainersVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "org.scalatest:scalatest_2.11:$scalaTestVersion")

    testCompile("junit:junit:4.11",
            "org.testcontainers:selenium:$testcontainersVersion",
            "org.testcontainers:mysql:$testcontainersVersion",
            "mysql:mysql-connector-java:$mysqlConnectorVersion",
            "org.mockito:mockito-all:$mockitoVersion"

    )
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from scaladoc
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'testcontainers-scala'
                packaging 'jar'
                description 'Scala wrapper for testcontainers-java'
                url 'https://github.com/dimafeng/testcontainers-scala'

                scm {
                    connection 'scm:git:git@github.com:dimafeng/testcontainers-scala.git'
                    developerConnection 'scm:git:git@github.com:dimafeng/testcontainers-scala.git'
                    url 'git@github.com:dimafeng/testcontainers-scala.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        name 'Dmitry Fedosov'
                        email 'dimafeng@gmail.com'
                    }
                }
            }
        }
    }
}

afterReleaseBuild.dependsOn uploadArchives